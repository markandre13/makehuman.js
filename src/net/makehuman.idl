enum MotionCaptureType {
    BODY,
    FACE,
    HAND
};

enum MotionCaptureEngine {
    NONE,
    MEDIAPIPE,
    LIVELINK,
    CHORDATA
};

// the API for now isn't very much OO but it works for now

// frontend is called by the backend
interface Frontend {
    // callback to receive raw chordata frame
    oneway void chordata(in sequence<octet> data);

    // callbacks to receive face data
    // names for the entries in faceLandmarks() blendshapes sequence
    // send at the beginning or whenever that list of names changes
    oneway void faceBlendshapeNames(in sequence<string> faceBlendshapeNames);
    oneway void faceLandmarks(
        in sequence<float> landmarks,
        in sequence<float> blendshapes,
        in sequence<float> transform,
        in unsigned long long timestamp_ms);

    oneway void poseLandmarks(
        in sequence<float> landmarks,
        in unsigned long long timestamp_ms);
};

interface VideoCamera2 {
    readonly attribute string id;
    attribute string name;
};

struct Range {
    unsigned long long start_ms;
    unsigned long long end_ms;
};

// backend is called by the frontend
interface Backend {
    // called by the frontend to register itself to receive data
    void setFrontend(in Frontend frontend);

    // selects the kind of data the frontend wants to receive
    void setEngine(in MotionCaptureType type, in MotionCaptureEngine engine);

    // quick hack to load and store files
    void save(in string filename, in string data);
    string load(in string filename);

    sequence<VideoCamera2> getVideoCameras();

    // quick hack to save/play video
    void record(in string filename);
    Range play(in string filename);
    void stop();

    void pause();
    void seek(in unsigned long long timestamp_ms);
};
